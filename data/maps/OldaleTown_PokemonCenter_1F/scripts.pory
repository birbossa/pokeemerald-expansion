raw`
.set LOCALID_NURSE, 1

OldaleTown_PokemonCenter_1F_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, OldaleTown_PokemonCenter_1F_OnTransition
	map_script MAP_SCRIPT_ON_RESUME, CableClub_OnResume
	.byte 0

OldaleTown_PokemonCenter_1F_OnTransition:
	setrespawn HEAL_LOCATION_OLDALE_TOWN
	call Common_EventScript_UpdateBrineyLocation
	end

OldaleTown_PokemonCenter_1F_EventScript_Nurse::
	setvar VAR_0x800B, LOCALID_NURSE
	call Common_EventScript_PkmnCenterNurse
	waitmessage
	waitbuttonpress
	release
	end

OldaleTown_PokemonCenter_1F_EventScript_Gentleman::
	msgbox OldaleTown_PokemonCenter_1F_Text_TrainersCanUsePC, MSGBOX_NPC
	end

OldaleTown_PokemonCenter_1F_EventScript_Boy::
	msgbox OldaleTown_PokemonCenter_1F_Text_PokemonCentersAreGreat, MSGBOX_NPC
	end

OldaleTown_PokemonCenter_1F_EventScript_Girl::
	lock
	faceplayer
	goto_if_set FLAG_SYS_POKEDEX_GET, OldaleTown_PokemonCenter_1F_EventScript_WirelessClubAvailable
	msgbox OldaleTown_PokemonCenter_1F_Text_WirelessClubNotAvailable, MSGBOX_DEFAULT
	release
	end

OldaleTown_PokemonCenter_1F_EventScript_WirelessClubAvailable::
	msgbox OldaleTown_PokemonCenter_1F_Text_TradedInWirelessClub, MSGBOX_DEFAULT
	release
	end

OldaleTown_PokemonCenter_1F_Text_TrainersCanUsePC:
	.string "That PC in the corner there is\n"
	.string "for any POKéMON TRAINER to use.\p"
	.string "Naturally, that means you're welcome\n"
	.string "to use it, too.$"

OldaleTown_PokemonCenter_1F_Text_PokemonCentersAreGreat:
	.string "POKéMON CENTERS are great!\p"
	.string "You can use their services as much\n"
	.string "as you like, and it's all for free.\l"
	.string "You never have to worry!$"

OldaleTown_PokemonCenter_1F_Text_WirelessClubNotAvailable:
	.string "The POKéMON WIRELESS CLUB on\n"
	.string "the second floor was built recently.\p"
	.string "But they say they're still making\n"
	.string "adjustments.$"

OldaleTown_PokemonCenter_1F_Text_TradedInWirelessClub:
	.string "The POKéMON WIRELESS CLUB on\n"
	.string "the second floor was built recently.\p"
	.string "I traded POKéMON right away.$"

`

script OldaleTown_PokemonCenter_1F_EventScript_Tutor 
{
	lockall
    faceplayer
    msgbox(Greetings, MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES)
	{
    	msgbox(Which_Mon)
        special(ChooseMonForMoveRelearner)
		waitstate
        if (var(VAR_0x8004) != PARTY_NOTHING_CHOSEN)
        {
        	if (var(VAR_0x8005) != 0)
			{
        		special(IsSelectedMonEgg)
            	if (var(VAR_RESULT) == FALSE)
				{
            		msgbox(Which_Move)
                	special(TeachMoveRelearnerMove)
					waitstate
                	msgbox(Come_Again)
                }
				else
				{
					msgbox(No_Eggs)
				}
			}
			else
			{
				msgbox(No_Eggs)
			}
    	}
		else
		{
			msgbox(Come_Back)
		}
    }
    else
	{
    	msgbox(Come_Back)
    }
    waitmessage
    releaseall
    end
}

text Greetings {
	format ("Hello young child!\NIt is I, the handy dandy move relearner. I can make your POKéMON remember moves they have forgotten.\NWould you like that?\NWould you like one of your POKéMON to remember a move?")
}

text Which_Mon {
	format("Which POKéMON would like to remember a move?")
}
    
text Come_Back {
	format("Okay, come see me anytime if you change your mind, child.")
}

text Which_Move {
	format("Which move should I teach?")
}
    
text Come_Again {
	format("Come back again anytime.")
}

text No_Eggs {
	format("I can't teach anything to that.")
}